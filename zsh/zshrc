# - [Moving to zsh](https://scriptingosx.com/2019/06/moving-to-zsh/)
# - [How to Configure zsh](https://zserge.com/posts/terminal/)
# - [Luke Smith's zshrc](https://gist.github.com/LukeSmithxyz/e62f26e55ea8b0ed41a65912fbebbe52)
#TODO: set window title
#TODO: git branch on right prompt. show * if "dirty"
#TODO: show return code


autoload -U compinit
zstyle ':completion:*' menu select
zstyle ':completion:*' completer _complete _correct _approximate 
compinit

# autoload -U colors vcs_info



################################################################################
# History
################################################################################
HISTFILE=~/.zsh/history
HISTSIZE=5000
SAVEHIST=5000

# Add commands to history as they are entered, don't wait for shell to exit
setopt INC_APPEND_HISTORY

# Add timestamp and duration of command to history
setopt EXTENDED_HISTORY

# Ignore duplicate commands in history
setopt HIST_IGNORE_ALL_DUPS

# Ignore commands that start with a whitespace
setopt HIST_IGNORE_SPACE

HISTORY_IGNORE='(ls|la|cd|pwd|exit)'

# Correct spelling of all arguments in the command line
setopt CORRECT_ALL

# Re-allow `fwd-i-search` via ctrl-s
#
# Typically in shell environments, ctrl-s is reserved for XON/XOFF flow control,
# an antiquated TTY-era signal/command. Disabling this should not pose a problem
# but, to be as safe as possible, it is disabled only within interactive shells.
#
#   - `[[ $- == *i* ]]` checks if interactive
#   - `stty -ixon` disables ctrl-s
#
# - https://unix.stackexchange.com/questions/73498/how-to-cycle-through-reverse-i-search-in-bash#73499
# - https://stackoverflow.com/questions/791765/unable-to-forward-search-bash-history-similarly-as-with-ctrl-r#791800
# - https://stackoverflow.com/questions/24623021/getting-stty-standard-input-inappropriate-ioctl-for-device-when-using-scp-thro#25391867
# - https://unix.stackexchange.com/questions/137842/what-is-the-point-of-ctrl-s
[[ $- == *i* ]] && stty -ixon




################################################################################
# Aliases                                                                      #
################################################################################
if [ -f ~/.shell_aliases ]; then
  source ~/.shell_aliases
fi




################################################################################
# Prompt
################################################################################
# https://stackoverflow.com/questions/55055662/zsh-theme-not-updating-from-function
# http://zsh.sourceforge.net/Doc/Release/Prompt-Expansion.html
# Colors: https://jonasjacek.github.io/colors/
precmd () {
  local DIRS="$(dirs)"
  
  #http://zsh.sourceforge.net/Guide/zshguide05.html#l121
  local LEADING_PATH="${DIRS%/*}"
  local WORKING_DIR="${DIRS##*/}"

  psvar[1]="$LEADING_PATH/"
  psvar[2]="$WORKING_DIR"
  
  if [ "$LEADING_PATH" = "$WORKING_DIR" ]; then
    psvar[1]=''
    if [ "$WORKING_DIR" = '' ]; then
      psvar[2]='/'
    fi
  fi  
}

# `%#` shows either `%` or `#` depending on if it's running with superuser
# privileges.
PROMPT='ðŸ‘» %K{magenta}%F{205}%1v%F{white}%2v%k%F{magenta}%#%f '




# Load zsh-syntax-highlighting; must be last.
source /usr/local/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh